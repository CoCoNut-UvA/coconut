
start phase startPhase {
    info = "The starting point"
    prefix = RT

    actions {
        pass scanAndParse;
        semantic;
        dynamic_backend;
        print;
    }
};

phase semantic {
    info = "Main part"
    prefix = SEA

    actions {
        buildSymbolTable;
        checkExistance;
        unwrapSetExpr;
    }
};

phase dynamic_backend {
    info = "Generating part for the dynamic backend"
    prefix = DDB

    actions {
        phase dynamic_nodes {
            info = "Traversal to generate all node related data"

            actions {
                dynamic_genNodeStructs;
            }
        };
    }
};

traversal dynamic_genNodeStructs {
    prefix = DGNS

    travdata = {
        int fill
    }
};

traversal checkExistance {
    prefix = CEX

    travdata = {
        int fill
    }
};

traversal buildSymbolTable {
    prefix = BST
    travdata = {
        int fill
    }
};

traversal unwrapSetExpr {
    prefix = USE
    travdata = {
        int fill
    }
};

traversal print {
    info = "Print the ast"
    prefix = PRT
    travdata = {
        int indent
    }
};


root node ast {
    children {
        iphase iphases;
        itraversal itraversals;
        ipass ipasses;
        inode inodes;
        inodeset inodesets;
        ste stable;
    }
};

nodeset action = { iphase, itraversal, ipass};
nodeset link = action | {inode};

node iactions {
    children {
        iactions next;
    }
    attributes {
        id reference {};
    }
};

node iphase {
    children {
        iactions iactions;
        iphase next;
    }
    attributes {
        id name { constructor };
        int is_start { constructor };
        int is_cycle {};
        string iinfo {};
        id iprefix {};
        id gate_func {};
    }
};

node itraversal {
    children {
        itraversal next;
        inode inodes;
    }
    attributes {
        id name { constructor };
        string iinfo {};
        id iprefix {};

    }
};

node ipass {
    children {
        ipass next;
    }
    attributes {
        id name { constructor };
        string iifno { constructor };
        id iprefix { constructor };
        id target_func {};
    }
};

node inode {
    children {
        inode next;
        child ichildren;
        attribute iattributes;
    }

    attributes {
        id name { constructor };
        string iifno { constructor };
        int is_root {};
    }
};

node inodeset {
    children {
        setexpr expr;
        id unpacked;
        inodeset next;
    }
    attributes {
        id name {};
        string iinfo {};
    }
};

enum child_type {
    prefix = CT
    values = {
        inode, inodeset
    }
};

node child {
    children {
        child next;
    }

    attributes {
        id name { constructor };
        id type_reference {};
        child_type type {};
        int in_constructor {};
        int is_mandatory {};
    }
};

node ste {
    children {
        ste next;
    }
    attributes {
        id key {};
        link value {};
    }
};

enum attribute_type {
    prefix = AT
    values = {
        link, link_or_enum, iint, istring, ibool
    }
};

enum setoperation_type {
    prefix = SO
    values = {
        iunion, intersect, difference
    }
};

node setreference {
    attributes {
        id reference {};
    }
};

node setliteral {
    children {
        setliteral next;
    }

    attributes {
        id reference {};
    }
};

node setoperation {
    children {
        setexpr left {constructor};
        setexpr right {constructor};
    }
    attributes {
        setoperation_type type { constructor };
    }
};

nodeset setexpr = {setoperation, setreference, setliteral};

node attribute {
    children {
        attribute next;
    }
    attributes {
        id name {};
        id type_reference {};
        attribute_type type {};
        string include_file {};
        int in_constructor {};
    }
};

node ienum {
    children {
        id vals {constructor};
    }
    attributes {
        id name {constructor};
        id iprefix {constructor};
        string iinfo { constructor };
    }
};

node id {
    children {
        id next {};
    }
    attributes {
        string orig { constructor };
        string lwr { constructor };
        string upr { constructor };
    }
};
